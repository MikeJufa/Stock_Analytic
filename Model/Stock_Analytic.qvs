SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='$#,##0.00;-$#,##0.00';
SET TimeFormat='h:mm:ss TT';
SET DateFormat='YYYY-MM-DD';
SET TimestampFormat='M/D/YYYY h:mm:ss[.fff] TT';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET CreateSearchIndexOnReload=1;
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:Î¼;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';


//Updated on Feb. 26, 2020

Directory lib://DataFiles/;

SET vNum = Num($1,'#,##0.0');
SET v%Num = Num($1,'#,##0.0%');
SET vMinDate = '=Num(Min(Date))';
SET vMaxDate = '=Num(Max(Date))';
SET vQVDName = 'Stock';
SET vStock = 'NFLX';

LET vTodayDate = num(Today());


//FOR EACH vStock in 'NFLX'
//,'TSLA','GOOG','AAPL','MSFT','FB','AMZN'
  StockList:
  Load
  		*
      , [Trade Date]&' - '&WeekDay as [Trade Date WD]
  ;
  Load
        *
      , '$(vStock)' as StockName
      , Year(Date) as ReportYear
      , Date(MonthStart(Date),'MMM-YYYY') as ReportYearMonth
      , WeekName(Date) as ReportWeek
      , Num(Date) as DateNum
      , WeekDay(Date) as WeekDay
      , $(vNum((Open-Close)*-1)) as OpenCloseDiff
      , $(v%Num((Open-Close)*-1/Open)) as OpenCloseDiff%
      , $(vNum(High-Low)) as HighLowDiff
      , $(v%Num((High-Low)/High)) as HighLowDiff%
      , $(vNum(Open - Peek(Close))) as OvernightDiff
      , $(v%Num((Open - Peek(Close))/Open)) as OvernightDiff%
      , Date(floor(Date),'YYYY.MM.DD') as [Trade Date]
  ;
  LOAD
        Date
      , $(vNum(Open)) as Open
      , $(vNum(High)) as High
      , $(vNum(Low)) as Low
      , $(vNum(Close)) as Close
      , $(vNum([Adj Close])) as [Adj Close]
      , Volume
FROM [$(vStock).csv]
(txt, codepage is 28591, embedded labels, delimiter is ',', msq)
;

WeekSum:
Load
	  ReportWeek
    , Sum(OpenCloseDiff+OvernightDiff) as WeeklyDiff
    , Sum(Volume) as WeeklyVolume
Resident StockList
Group by ReportWeek
;



//Date Ranges
Ranges:
Load
	  Range
    , Date(Evaluate(RangeStart)) as RangeStart
    , Date(Evaluate(RangeEnd)) as RangeEnd
;
Load * Inline
[
Range; RangeStart; RangeEnd
Past 0 Month; MonthStart(vTodayDate); MonthEnd(vTodayDate)
Past 1 Month; MonthStart(vTodayDate,-1); MonthEnd(vTodayDate)
Past 2 Month; MonthStart(vTodayDate,-2); MonthEnd(vTodayDate)
Past 3 Month; MonthStart(vTodayDate,-3); MonthEnd(vTodayDate)
Past 6 Month; MonthStart(vTodayDate,-6); MonthEnd(vTodayDate)
](delimiter is ';')
;

Join(Ranges)

IntervalMatch([Trade Date])

Load RangeStart, RangeEnd Resident Ranges;

Drop Fields RangeStart, RangeEnd;
